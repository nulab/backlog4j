plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'org.jreleaser' version '1.17.0'
}

group = 'com.nulab-inc'
archivesBaseName = 'backlog4j'
version = project.property('version')

java {
    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
    testImplementation 'org.hamcrest:hamcrest-library:2.2'
    testImplementation 'org.exparity:hamcrest-date:2.0.8'
    testImplementation 'ch.qos.logback:logback-classic:1.5.18'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.12.0'

    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.19.0'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.19.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.19.0'
    implementation 'org.slf4j:slf4j-api:2.0.17'
}

test {
    useJUnitPlatform()
}

javadoc {
    options.locale = 'en_US'
    options.encoding = 'UTF-8'
    options.addStringOption('Xdoclint:none', '-quiet')
    title = 'Backlog4j ' + version + ' API'
}

jar {
    manifest {
        attributes('Implementation-Title': archivesBaseName,
                'Implementation-Version': archiveVersion,
                'Automatic-Module-Name': archiveBaseName)
    }
}

configurations {
    deployerJars
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'Backlog4j'
                description = 'Backlog4j is a Backlog binding library for Java.'
                url = 'https://github.com/nulab/backlog4j'
                scm {
                    connection = 'https://github.com/nulab/backlog4j.git'
                    developerConnection = 'https://github.com/nulab/backlog4j.git'
                    url = 'https://github.com/nulab/backlog4j.git'
                }
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/license/mit/'
                    }
                }
                developers {
                    developer {
                        id = 'uchida'
                        name = 'Yuichi Uchida'
                        email = 'uchida@nulab.com'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = "Staging"
            url = uri("$buildDir/staging-deploy")
        }
    }
}